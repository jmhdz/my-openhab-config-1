rule "Thinkingcleaner JSON"
when
    Item EG_Vorzimmer_Roomba_json received update
then
// status.json:
//{
//  "action" : "status",
//  "result" : "success",
//  "status" : {
//    "name" : "petsi",
//    "battery_charge" : 100,
//    "capacity" : "2696",
//    "cleaner_state" : "st_base_trickle",
//    "cleaning" : "0",
//    "schedule_serial_number" : "0",
//    "near_homebase" : "0"
//  }
//}

    var String json = EG_Vorzimmer_Roomba_json.state.toString
    logInfo("Roomba", json)

    EG_Vorzimmer_Roomba_status.postUpdate(   transform("JSONPATH","$.status.cleaner_state",  json) )
    EG_Vorzimmer_Roomba_battery.postUpdate(  transform("JSONPATH","$.status.battery_charge", json) )
    EG_Vorzimmer_Roomba_capacity.postUpdate( transform("JSONPATH","$.status.capacity",       json) )

// Switch Items
    if ( transform("JSONPATH","$.status.near_homebase",json) == "1" ) {
      EG_Vorzimmer_Roomba_homebase.postUpdate(ON)
    } else {
      EG_Vorzimmer_Roomba_homebase.postUpdate(OFF)
    }

    if ( transform("JSONPATH","$.status.cleaning", json) == "1" ) {
      EG_Vorzimmer_Roomba_cleaning.postUpdate(ON)
    } else {
      EG_Vorzimmer_Roomba_cleaning.postUpdate(OFF)
    }
end

